plugins {
    id 'java'
    id 'application'
    id "com.linecorp.thrift-gradle-plugin" version "0.6.1"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    implementation 'org.apache.httpcomponents.client5:httpclient5-fluent:5.2.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
    implementation 'org.apache.thrift:libthrift:0.19.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

application {
    mainClass = 'org.leadMerger.Main'
}

compileThrift {
    thriftExecutable "/usr/bin/thrift"
    sourceDir "$projectDir/src/main/thrift"
    outputDir "$projectDir/generated/java"
    nowarn true
    strict true
    verbose true
    recurse true
    debug true
    createGenFolder false
    generator 'java', 'private-members'
}

sourceSets.main.java.srcDirs += ["$projectDir/generated/java"]

// Copy central conf file to generated-resources
task copySalesforceConf(type: Copy) {
    from "$rootDir/conf/salesforce.properties"
    into "$buildDir/generated-resources"
}

sourceSets.main.resources.srcDir "$buildDir/generated-resources"

// Copy conf file before handling ressources
processResources.dependsOn copySalesforceConf

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    manifest {
        attributes 'Main-Class': 'org.leadMerger.Main'
    }
}

shadowJar {
    archiveBaseName.set('leadMerger-all')
    manifest {
        attributes 'Main-Class': 'org.leadMerger.Main'
    }
    mergeServiceFiles()
}
